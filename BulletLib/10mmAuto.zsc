// ------------------------------
// CREATED BY SWAMPYRAD
// ------------------------------

const enc_10=1.2;
const enc_10_loaded=enc_10/1.3;

/* for reference, 10mm should be just a bit better than 355,
almost twice as powerful as 9mm, but high recoil due to incredibly hot loads.*/
class HDB_10:HDBulletActor{
	default{
		pushfactor 0.25;//high-velocity, better grouping
		mass 120; // 2g more than 355
		speed HDCONST_MPSTODUPT*400;//50m per sec faster than 355
		accuracy 200;//Round ball bullet
		stamina 1000;//10mm, 1000
		woundhealth 18;//20% stronger than 355
		hdbulletactor.hardness 3;//Copper FMJ rounds
	}
}

class HD10mAmmo:HDRoundAmmo{//10mm ammo pickup
	default{
		+inventory.ignoreskill
		+cannotpush
		+forcexybillboard
		+rollsprite +rollcenter
		+hdpickup.multipickup
		xscale 0.7;
		yscale 0.6;
		inventory.pickupmessage "Picked up a 10mm round.";
		hdpickup.refid "10M";
		tag "$TAG_10MMAUTO";
		hdpickup.bulk enc_10;
		inventory.icon "T10MA0";
	}

	override void SplitPickup(){
		SplitPickupBoxableRound(10,100,"HD10mBoxPickup","T10MA0","PR10A0");
	}
	states{
	spawn:
		PR10 A -1;
		T10M A -1;
	}
}

//(primers and bullet lead can be cannibalized from 9mm rounds)
class TenMilBrass:HDAmmo{
	default{
		+inventory.ignoreskill +forcexybillboard +cannotpush
		+hdpickup.multipickup
		+hdpickup.cheatnogive
		height 16;radius 8;
		tag "$TAG_10MMCASING";
		hdpickup.refid "c10";
		hdpickup.bulk ENC_776B;
		xscale 0.7;yscale 0.8;
		inventory.pickupmessage "Picked up some 10mm brass.";
		inventory.icon "CS10B0";
	}
	states{
	spawn:
		CS10 B -1;
		stop;
	}
}

class HDSpent10mm:HDUPK{
	default{
		+missile
		+hdupk.multipickup
		height 4;radius 2;
		bouncetype "doom";
		hdupk.pickuptype "TenMilBrass";
		hdupk.pickupmessage "Picked up a 10mm casing.";

		bouncesound "misc/casing";
		bouncefactor 0.4;
		xscale 0.7;yscale 0.8;
		maxstepheight 0.6;
	}
	vector3 lastvel;
	override void Tick(){
		if(!isFrozen())lastvel=vel;
		super.Tick();
	}
	states{
	spawn:
		CS10 A 2{
			if(bseesdaggers)angle-=45;else angle+=45;
			if(pos.z-floorz<2&&abs(vel.z)<2.)setstatelabel("death");
		}wait;
	death:
		CS10 B -1{
			bmissile=false;
			vel.xy+=(pos.xy-prev.xy)*max(abs(vel.z),abs(prev.z-pos.z),1.);
			if(vel.xy==(0,0)){
				double aaa=angle-90;
				vel.x+=cos(aaa);
				vel.y+=sin(aaa);
			}else{
				A_FaceMovementDirection();
				angle+=90;
			}
			let gdb=getdefaultbytype(pickuptype);
			A_SetSize(gdb.radius,gdb.height);
			return;
		}stop;
	}
}

class HDLoose10mm:HDSpent10mm{
	default{
		bouncefactor 0.5;
	}
	states{
	death:
		TNT1 A 1{
			actor a=spawn("HD10mAmmo",self.pos,ALLOW_REPLACE);
			a.roll=self.roll;a.vel=self.vel;
		}stop;
	}
}

class HD10mBoxPickup:HDUPK{
	default{
		//$Category "Ammo/Hideous Destructor/"
		//$Title "Box of 10mm"
		//$Sprite "BX10A0"

		scale 0.4;
		hdupk.amount 100;
		hdupk.pickupsound "weapons/pocket";
		hdupk.pickupmessage "Picked up some 10mm ammo.";
		hdupk.pickuptype "HD10mAmmo";
	}
	states{
	spawn:
		BX10 A -1;
	}
}